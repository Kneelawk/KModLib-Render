pluginManagement {
    repositories {
        maven {
            name = 'Fabric'
            url = 'https://maven.fabricmc.net/'
        }
        maven {
            name = 'Architectury'
            url = 'https://maven.architectury.dev/'
        }
        maven {
            name = 'NeoForged'
            url = 'https://maven.neoforged.net/releases/'
        }
        mavenCentral()
        gradlePluginPortal()
    }
    plugins {
        id 'fabric-loom' version settings.loom_version
        id 'architectury-plugin' version settings.architectury_version
        id 'dev.architectury.loom' version settings.architectury_loom_version
        id 'com.github.johnrengelman.shadow' version settings.shadow_version
    }
}

rootProject.name = 'kmodlib-all'

void module(String name) {
    String projectName = ":kmodlib-$name"
    include(projectName)
    project(projectName).projectDir = file("modules/${name.replace(':', '/')}")
}

module 'blockmodel'
module 'overlay-fabric'
module 'overlay-neoforge'
module 'renderlayer'
module 'rendertag'

module 'all-fabric'
